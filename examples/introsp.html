<HEAD><TITLE>introsp.sp</TITLE></HEAD>
<BODY>
<HR><DIV ALIGN="center"><H1> File : introsp.sp </H1></DIV><HR>
<DIV ALIGN="center">
<TABLE CELLSPACING="0" CELLPADDING="3" WIDTH="80%" SUMMARY="">
<TR>
<TD BGCOLOR="black"><SPAN STYLE="color: #00CC00">
<PRE>
$ spar introsp.sp
System Name: SYSTEM_NAME_SPARFORTE
System Version: 1.3
bloop exists
abs() exists
abs(bloop) =  5
bloop                | -5                           | identifier of the type integer
There are 1 integer variables
Their sum is -5

</PRE>
</SPAN>
</TD>
</TR>
</TABLE>
</DIV>
<HR>
<PRE>
#!/usr/local/bin/spar

<b>pragma</b> annotate( summary, "introsp" )
              @( description, "Verify the version/revision of your currently running" )
              @( description, "(compiler/interpreter/byte-compiler/runtime environment/" )
              @( description, "whatever your language uses) and exit if it is too old." )
              @( description, "check whether the variable 'bloop' exists and whether the" )
              @( description, " math-function 'abs()' is available and if yes compute" )
              @( description, "abs(bloop).  Extra credit: Report the number of integer" )
              @( description, "variables in global scope, and their sum." )
              @( category, "tutorials" )
              @( author, "Ken O. Burtch" )
              @( see_also, "http://rosettacode.org/wiki/Introspection" );
<b>pragma</b> license( unrestricted );

<b>procedure</b> introsp <b>is</b>
   bloop : integer := -5;
   e     : string;
   s     : string;
<b>begin</b>

   <FONT COLOR=green><EM>-- Verify Interpreter</EM></FONT>

   ? "System Name: " &amp; System.System_Name;
   ? "System Version: " &amp; System.System_Version;

   <FONT COLOR=green><EM>-- get initial environment</EM></FONT>

   e := `<b>env</b>;`;

   <FONT COLOR=green><EM>-- the bloop test</EM></FONT>

   s := `echo "$e" | grep "^bloop\ " ;` ;
   <b>if</b> strings.length( s ) &gt; 0 <b>then</b>
      ? "bloop exists";
      s := `echo "$e" | grep "^abs\ " ;` ;
      <b>if</b> strings.length( s ) &gt; 0 <b>then</b>
         ? "abs() exists";
         ? "abs(bloop) = " &amp; strings.image( <b>abs</b>( bloop ) );
      <b>else</b>
         ? "abs does not exist";
      <b>end</b> <b>if</b>;
   <b>else</b>
      ? "bloop does not exist";
   <b>end</b> <b>if</b>;

   <FONT COLOR=green><EM>-- count global integers and their sum</EM></FONT>
   <FONT COLOR=green><EM>--</EM></FONT>
   <FONT COLOR=green><EM>-- bit of a cheat...we're counting their value at script startup</EM></FONT>

   <b>declare</b>
     pos : natural := 0;
     ln  : string;
     ch  : character;
     cnt : natural := 0;
     sum_total : integer := 0;
   <b>begin</b>
     <b>while</b> pos &lt; strings.length( e ) <b>loop</b>
       ln := "";
       <b>loop</b>
         pos := @+1;
         <b>exit</b> <b>when</b> pos &gt; strings.length( e );
         ch := strings.element( e, positive( pos ) );
         <b>exit</b> <b>when</b> ch = ASCII.LF;
         ln := @ &amp; ch;
       <b>end</b> <b>loop</b>;
       <b>if</b> strings.index( ln, "identifier of the type integer" ) &gt; 0 <b>then</b>
         ? ln;
         cnt := @ + 1;
         s := strings.trim( strings.field( ln, 2, "|" ), trim_end.both );
         <b>if</b> strings.length( s ) &gt; 0 <b>then</b>
            sum_total := @ + numerics.value( s );
         <b>end</b> <b>if</b>;
       <b>end</b> <b>if</b>;
     <b>end</b> <b>loop</b>;
     ? "There are" &amp; strings.image( cnt ) &amp; " integer variables";
     ? "Their sum is " &amp; strings.image( sum_total );
   <b>end</b>;

<b>end</b> introsp;

<FONT COLOR=green><EM>-- VIM editor formatting instructions</EM></FONT>
<FONT COLOR=green><EM>-- vim: ft=spar</EM></FONT>

</PRE></BODY></HTML>
